import com.github.spotbugs.snom.SpotBugsTask

buildscript {
    ext {
        hamcrestVersion = '2.2'
        jacocoVersion = '0.8.7'
        junitVersion = '5.8.2'
        lombokVersion = '1.18.22'
        spotbugsVersion = '4.5.3'
    }
}

plugins {
    id 'com.github.spotbugs' version '5.0.5'
    id 'jacoco'
    id 'java'
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    annotationProcessor group: 'org.projectlombok', name: 'lombok',
            version: lombokVersion
    compileOnly group: 'org.projectlombok', name: 'lombok',
            version: lombokVersion
    testCompileOnly group: 'org.projectlombok', name: 'lombok',
            version: lombokVersion
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok',
            version: lombokVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter',
            version: junitVersion
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library',
            version: hamcrestVersion
}

defaultTasks 'clean', 'build'

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    options.compilerArgs += ['-Werror', '-Xlint:all', '-Xlint:-processing']
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.withType(SpotBugsTask) {
    reports {
        html.enabled true
        xml.enabled false
    }
}

clean {
    delete 'out'
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            limit {
                minimum = 1.00
            }
        }
    }
}

spotbugs {
    toolVersion = spotbugsVersion
}

tasks.withType(SpotBugsTask) {
    showProgress = false

    reports {
        xml.enabled false
        html.enabled true
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}
